bool = { "true" | "false" }

digits = { ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }
float = @{ int ~ "." ~ digits? }
int = @{ ("+" | "-")? ~ digits }
num = { float | int }

comment = { ";" ~ ( !NEWLINE ~ ANY )* }

string_chars = { ( "\\\"" | ( !"\"" ~ ANY ) )* }
string = { "\"" ~ string_chars ~ "\"" }

whitespace = _{ NEWLINE | " " }
keyword = { ":" ~ (!whitespace ~ ANY)* }

symbol = { (!whitespace ~ !"(" ~ !")" ~ !"\"" ~ !"'" ~ ANY)+ }

elems = { value? ~ ( whitespace+ ~ value )* }
list = { "(" ~ elems ~ ")" }
vec = { "[" ~ elems ~ "]" }
map = { "{" ~ elems ~ "}" }

quote = { "'" ~ value }
quasiquote = { "`" ~ value }
unquote = { "~" ~ value }
splice_unquote = { "~@" ~ value }

value = {
    list |
    map |
    vec |
    quote |
    quasiquote |
    unquote |
    splice_unquote |
    comment |
    bool | 
    num |
    string |
    keyword |
    symbol
}

escaped_sigil = { "@@" }
text = {  (!"@" ~ ANY)+ }
file = { (escaped_sigil | ("@" ~ value) | text)+ }